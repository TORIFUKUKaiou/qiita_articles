---
title: é‡çƒã®ãƒœãƒ¼ãƒ«ã‚«ã‚¦ãƒ³ãƒˆï¼ˆã©ã†æ›¸ãï¼‰(Elixir)
tags:
  - Elixir
private: false
updated_at: '2021-01-06T23:15:24+09:00'
id: 27bdd4dd71eaebd9d410
organization_url_name: fukuokaex
slide: false
ignorePublish: false
---
# ã¯ã˜ã‚ã«
- [Elixir](https://elixir-lang.org/) æ¥½ã—ã‚“ã§ã„ã¾ã™ã‹ :bangbang::bangbang::bangbang:
- @Nabetaniã•ã‚“ã®ã€Œ[ã‚ªãƒ•ãƒ©ã‚¤ãƒ³ãƒªã‚¢ãƒ«ã‚¿ã‚¤ãƒ ã©ã†æ›¸ãç¬¬ä¸‰å›ã®å‚è€ƒå•é¡Œ](https://qiita.com/Nabetani/items/ebd8a56b41711ba459f9)ã€ã‚’[Elixir](https://elixir-lang.org/)ã§ã‚„ã£ã¦ã¿ã¾ã—ãŸ
    - @obelisk68 ã•ã‚“ã®ã€Œ[é‡çƒã®ãƒœãƒ¼ãƒ«ã‚«ã‚¦ãƒ³ãƒˆï¼ˆã©ã†æ›¸ãï¼‰](https://qiita.com/obelisk68/items/73fb67656c92437ee38f)ã€ã§å•é¡Œã®å­˜åœ¨ã‚’çŸ¥ã‚Šã¾ã—ãŸ

# æº–å‚™
- [Elixir](https://elixir-lang.org/) ã‚’ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«ã—ã¾ã—ã‚‡ã†
- æ‰‹å‰å‘³å™Œã§ã™ãŒã€[ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«](https://qiita.com/torifukukaiou/items/d04d0273749c41eb50af#0-%E3%82%A4%E3%83%B3%E3%82%B9%E3%83%88%E3%83%BC%E3%83%AB)ãªã©ã‚’ã”å‚ç…§ãã ã•ã„

## ã†ã¾ãã„ã‹ãªã‹ã£ãŸã‚‰
- ä½•äº‹ã«ã‚‚æº–å‚™ãŒè‚å¿ƒã§ã™
- ã“ã“ãŒä¸€ç•ªã¤ã¾ã‚‰ãªã„ã—ã€è¬ã«ãƒãƒã£ã¦ã—ã¾ã†ã“ã¨ãŒå¤šã„ã®ã§ã™ãŒã€ãŒã‚“ã°ã£ã¦ãã ã•ã„ï¼
- ã†ã¾ãã„ã‹ãªã‹ã£ãŸã‚‰ã€**æ€ã„åˆ‡ã£ã¦åƒ•ã®èƒ¸ã«é£›ã³è¾¼ã‚“ã§æ¥ã¦ã»ã—ã„** (by é•·å¶‹èŒ‚é›„ èª­å£²ã‚¸ãƒ£ã‚¤ã‚¢ãƒ³ãƒ„çµ‚èº«åèª‰ç›£ç£)
    - [elixirjp.slack.com slack workspace](https://elixirjp.slack.com/join/shared_invite/enQtODE0NjM3NTIyNTMzLTU5NmViZDE4N2Q3MGUyMmI5YTdlNmQ2ZDI4ZDgxZGZiYTVlYmJjOTMzYzk2NGUyMjBhMTBiNDdjYTg3ZjhmYWI)ã‹[NervesJP workspace](https://join.slack.com/t/nerves-jp/shared_invite/enQtNzc0NTM1OTA5MzQ1LTg5NTAyYThiYzRlNDRmNDIwM2ZlZTJiZDc1MmE5NTFjYzA5OTE4ZTM5OWQxODFhZjY1NWJmZTc4NThkMjQ1Yjk)ã«å…¥ã£ã¦ãã¦ã„ãŸã ã„ã¦ã€`@torifukukaiou`ã¸ã”è³ªå•ãã ã•ã„
    - ãŸã¨ãˆç§ãŒç­”ãˆã‚‰ã‚Œãªãã¦ã‚‚ã€ãƒã‚¸ã¿ã‚“ãªè¦ªåˆ‡ã§å„ªã—ã„äººãŒå¤šã„ã®ã§ã€ãã£ã¨è§£æ±ºã§ãã‚‹ã§ã—ã‚‡ã†:bangbang:

# mix new

```
$ mix new ball_count
$ cd ball_count
```

# ã‚½ãƒ¼ã‚¹ã‚³ãƒ¼ãƒ‰ã‚’æ›¸ã

```elixir:lib/ball_count.ex
defmodule BallCount do
  @doc """

  ## Examples

      iex> BallCount.solve("s")
      "010"

      iex> BallCount.solve("sss")
      "010,020,100"

      iex> BallCount.solve("bbbb")
      "001,002,003,000"

      iex> BallCount.solve("ssbbbb")
      "010,020,021,022,023,000"

      iex> BallCount.solve("ssbbbb")
      "010,020,021,022,023,000"

      iex> BallCount.solve("hsbhfhbh")
      "000,010,011,000,010,000,001,000"

      iex> BallCount.solve("psbpfpbp")
      "100,110,111,200,210,000,001,100"

      iex> BallCount.solve("ppp")
      "100,200,000"

      iex> BallCount.solve("ffffs")
      "010,020,020,020,100"

      iex> BallCount.solve("ssspfffs")
      "010,020,100,200,210,220,220,000"

      iex> BallCount.solve("bbbsfbppp")
      "001,002,003,013,023,000,100,200,000"

      iex> BallCount.solve("sssbbbbsbhsbppp")
      "010,020,100,101,102,103,100,110,111,100,110,111,200,000,100"

      iex> BallCount.solve("ssffpffssp")
      "010,020,020,020,100,110,120,200,210,000"
  """
  def solve(input) do
    String.codepoints(input)
    |> Enum.reduce([{0, 0, 0}], fn c, [head | _] = acc ->
      [do_solve(c, head) | acc]
    end)
    |> Enum.reverse()
    |> tl()
    |> Enum.map(&Tuple.to_list/1)
    |> Enum.map(&Enum.join/1)
    |> Enum.join(",")
  end

  defp do_solve("s", {2, 2, _}), do: {0, 0, 0}

  defp do_solve("s", {out, 2, _}), do: {out + 1, 0, 0}

  defp do_solve("s", {out, strike, ball}), do: {out, strike + 1, ball}

  defp do_solve("b", {out, _, 3}), do: {out, 0, 0}

  defp do_solve("b", {out, strike, ball}), do: {out, strike, ball + 1}

  defp do_solve("h", {out, _, _}), do: {out, 0, 0}

  defp do_solve("f", {out, 2, ball}), do: {out, 2, ball}

  defp do_solve("f", {out, strike, ball}), do: {out, strike + 1, ball}

  defp do_solve("p", {2, _, _}), do: {0, 0, 0}

  defp do_solve("p", {out, _, _}), do: {out + 1, 0, 0}
end
```

# [Doctests](https://elixir-lang.org/getting-started/mix-otp/docs-tests-and-with.html#doctests)

- `## Examples`ã®ä¸‹ã«ã‚ã‚‹ã‚‚ã®ã¯ã€[Doctests](https://elixir-lang.org/getting-started/mix-otp/docs-tests-and-with.html#doctests)ã¨å‘¼ã°ã‚Œã‚‹ã‚‚ã®ã§ãƒ†ã‚¹ãƒˆã§ãã‚‹ã‚“ã§ã™ :bangbang::bangbang::bangbang:

```
$ mix test
..............

Finished in 0.07 seconds
13 doctests, 1 test, 0 failures
```

# Wrapping Up ğŸğŸğŸğŸğŸ
- Pipe operator [|>](https://hexdocs.pm/elixir/Kernel.html#%7C%3E/2)ã€[Pattern matching](https://elixir-lang.org/getting-started/pattern-matching.html)ã€[Enum](https://hexdocs.pm/elixir/Enum.html#content)ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã®ã„ã‚ã„ã‚ãªé–¢æ•°ã€[Doctests](https://elixir-lang.org/getting-started/mix-otp/docs-tests-and-with.html#doctests)ãƒ¼ãƒ¼[Elixir](https://elixir-lang.org/)ã®è‰¯ã•ãŒã‚ˆãã§ã‚‹ã„ã„å•é¡Œã§ã—ãŸ :bangbang:
- ã¿ã‚“ãªã¡ãŒã£ã¦ã€€ã¿ã‚“ãªã„ã„(é‡‘å­ã¿ã™ã‚)
    - ã¿ãªã•ã‚“ã‚‚ãŠå¥½ããªãƒ—ãƒ­ã‚°ãƒ©ãƒŸãƒ³ã‚°è¨€èªã§æ›¸ã„ã¦ã¿ã¦ãã ã•ã„ï¼
- Enjoy [Elixir](https://elixir-lang.org/) :rocket::rocket::rocket:
