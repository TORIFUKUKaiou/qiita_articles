---
title: Elixirでトリボナッチ数列の問題を解いてみた(制限時間10分)
tags:
  - Elixir
private: false
updated_at: '2020-06-22T21:20:38+09:00'
id: d5a6639edf541539ac3a
organization_url_name: fukuokaex
slide: false
---
# はじめに
- @keisuke_oyatu さんの[Rubyでトリボナッチ数列の問題を解いてみた(制限時間10分)](https://qiita.com/keisuke_oyatu/items/43c0100f885c2050b956)を拝見しまして、私はぜひ[Elixir](https://elixir-lang.org/)でやってみようとおもいました
- [Elixir](https://elixir-lang.org/)は`Elixir 1.10.3 (compiled with Erlang/OTP 23)`を使いました

## ハイライト

```elixir:lib/tribonacci.ex
defmodule Tribonacci do
  def number(n), do: do_number(n, {1, 3, 7})

  defp do_number(n, {n_3, _, _}) when n <= 0, do: n_3

  defp do_number(n, {n_3, n_2, n_1}) do
    do_number(n - 1, {n_2, n_1, n_3 + n_2 + n_1})
  end
end
```

# 0. インストールとプロジェクトの作成
- まずは[Elixir](https://elixir-lang.org/)をインストールしましょう
    - 手前味噌ですが、[インストール](https://qiita.com/torifukukaiou/items/d04d0273749c41eb50af#0-%E3%82%A4%E3%83%B3%E3%82%B9%E3%83%88%E3%83%BC%E3%83%AB) などを参考にしてください
- インストールができましたら以下のコマンドでプロジェクトを作ります

```console
$ mix new tribonacci
$ cd tribonacci
```

# 1. ソースコードを書く (その1)

```elixir:lib/tribonacci.ex
defmodule Tribonacci do
  @doc """
  Tribonacci number.

  ## Examples

      iex> Tribonacci.number(0)
      1
      iex> Tribonacci.number(1)
      3
      iex> Tribonacci.number(2)
      7
      iex> Tribonacci.number(3)
      11
      iex> Tribonacci.number(4)
      21

  """
  def number(0), do: 1
  def number(1), do: 3
  def number(2), do: 7
  def number(n), do: number(n - 3) + number(n - 2) + number(n - 1)
end
```

- `## Examples`のコメント部分は[Doctest](https://elixir-lang.org/getting-started/mix-otp/docs-tests-and-with.html#doctests)と呼ばれるものでテストができます

```console
$ mix test
```

- `mix new`したときに元からできている`hello/0`関数はこの記事では消していますが、そのまま残していてもかまいません
- `test/tribonacci_test.exs`に`hello/0`関数のテストがありますので、`hello/0`関数を消した場合には消すなり、気にしないことにするなりしてください

## 50番目(インデックス49)を求めてみましょう

```
$ iex -S mix

iex> Tribonacci.number(49)
```

- 私のパソコンでは計算に時間がかかって待ちきれませんでした
- `Ctl+C`を2回押して強制終了しましょう

# 2. ソースコードを書く (その2)

```elixir:lib/tribonacci.ex
defmodule Tribonacci do
  @doc """
  Tribonacci number.

  ## Examples

      iex> Tribonacci.number(0)
      1
      iex> Tribonacci.number(1)
      3
      iex> Tribonacci.number(2)
      7
      iex> Tribonacci.number(3)
      11
      iex> Tribonacci.number(4)
      21

  """
  def number(n), do: do_number(n, {1, 3, 7})

  defp do_number(n, {n_3, _, _}) when n <= 0, do: n_3

  defp do_number(n, {n_3, n_2, n_1}) do
    do_number(n - 1, {n_2, n_1, n_3 + n_2 + n_1})
  end
end
```

- テストがあるので修正が正しいことを確かめられます

```console
$ mix test
```

- [Doctest](https://elixir-lang.org/getting-started/mix-otp/docs-tests-and-with.html#doctests)の部分がパスしていることでしょう

## あらためて50番目(インデックス49)を求めてみましょう

```
$ iex -S mix

iex> Tribonacci.number(49)
17079382868243
```

:tada::tada::tada:

- `IEx`は `System.halt`で終わらせましょう


# Wrapping Up
- お好みの言語でトリボナッチ数列をお楽しみください
- **Enjoy!**
 
